apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def GROUP_ID = GROUP_NAME
def ARTIFACT_ID = ARTIFACT_NAME
def VERSION = VERSION_NAME

def getUserName() {
    return hasProperty('USER') ? USER : System.getenv("user")
}

def getApiKey() {
    return hasProperty('API_KEY') ? API_KEY : System.getenv("apikey")
}

def getPassPhrase() {
    return hasProperty('PASSPHRASE') ? PASSPHRASE : System.getenv("passphrase")
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION
            artifact sourcesJar
            artifact("$buildDir/outputs/aar/app-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = getUserName()
    key = getApiKey()
    publications = ['MyPublication']
    pkg {
        repo = 'android-releases'
        name = 'connectivity'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/AcnNuriSezgin/Connectivity.git'
        publicDownloadNumbers = true
        publish = true
        version {
            name = VERSION
            desc = 'Network status listener'
            gpg {
                sign = true
                passphrase = getPassPhrase()
            }
        }
    }
}